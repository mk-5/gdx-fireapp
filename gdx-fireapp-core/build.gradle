apply plugin: "java"
apply plugin: "groovy"
apply plugin: "maven-publish"

sourceCompatibility = 1.7
targetCompatibility = 1.7

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceSets.main.java.srcDirs = ["src/"]
sourceSets.test.java.srcDirs = ["tests/java"]
sourceSets.test.groovy.srcDirs = ["tests/groovy"]

jar {
    from sourceSets.main.allSource
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

dependencies {
    implementation libs.libgdx.core
    testImplementation libs.junit
    testImplementation libs.powermock.junit
    testImplementation libs.powermock.junitRule
    testImplementation libs.powermock.api
    testImplementation libs.powermock.xstream
    testImplementation libs.systemrules
    testImplementation libs.spock.groovyAll
    testImplementation libs.spock.spockCore
    testImplementation libs.spock.byteBuddy
}

eclipse.project {
    name = "gdx-fireapp-core"
}

ext {
    appModuleName = 'GDX Fireapp Core'
}

apply from: "" + project.rootDir + "/gradle/javadoc.gradle"
apply from: "" + project.rootDir + "/gradle/pom.gradle"

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            version = rootProject.version
            artifactId = 'gdx-fireapp-core'
            artifact jar
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', libraryDescription)
                root.appendNode('name', libraryName)
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

apply from: "" + project.rootDir + "/gradle/bintray.gradle"