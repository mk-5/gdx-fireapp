apply plugin: "com.android.library"
apply plugin: "maven-publish"

dependencies {
    implementation project(":gdx-fireapp-core")
    api libs.firebase.core, libs.firebase.storage, libs.firebase.auth, libs.firebase.database
    api libs.crashlytics
    api libs.libgdx.android
    api libs.playservices.auth
    testImplementation libs.powermock.junit
    testImplementation libs.powermock.junitRule
    testImplementation libs.powermock.api
    testImplementation libs.powermock.xstream
    testImplementation libs.robolectric
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.0"
    }

    testOptions {
        unitTests {
            returnDefaultValues = false
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

}

ext {
    appModuleName = 'GDX Fireapp Android'
}

apply from: "" + project.rootDir + "/gradle/android/javadoc.gradle"
apply from: "" + project.rootDir + "/gradle/pom.gradle"


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            version = rootProject.version
            artifactId = 'gdx-fireapp-android'
            artifact("$buildDir/outputs/aar/gdx-fireapp-android-release.aar")
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', libraryDescription)
                root.appendNode('name', libraryName)
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

apply from: "" + project.rootDir + "/gradle/bintray.gradle"

publishToMavenLocal.dependsOn.add("assembleRelease")
